* Note Taking

Org-mode and note-taking configuration for knowledge management.


#+begin_src emacs-lisp :tangle ~/.emacs.d/elisp/settings/note-conf.el
  ;;; note-conf.el --- Summary  -*- lexical-binding: t; -*-
  ;;; Commentary:
  ;;; Code:
  (use-package org-contrib)
  (use-package ob-typescript)
  (use-package ob-powershell)
  (use-package ob-http)
  (use-package ob-restclient)
  (use-package org
    :pin org
    :commands (org-capture org-agenda)
    :custom
    (org-agenda-start-with-log-mode t)
    (org-catch-invisible-edits 'show)
    (org-edit-timestamp-down-means-later t)
    (org-export-coding-system 'utf-8)
    (org-latex-compiler "xelatex")
    (org-latex-pdf-process '("latexmk -xelatex -synctex=1 -f -interaction=nonstopmode -output-directory=%o %f"))
    (org-export-kill-product-buffer-when-displayed t)
    (org-fast-tag-selection-single-key 'expert)
    (org-hide-emphasis-markers t)
    (org-html-validation-link nil)
    (org-image-actual-width '(450))
    (org-log-done 'time)
    (org-log-into-drawer t)
    (org-pretty-entities nil)
    (org-startup-indented nil)
    (org-startup-with-inline-images t)
    (org-display-remote-inline-images 'download)
    (org-tags-column 80)
    (org-src-window-setup 'current-window)
    :config
    (require 'org-tempo)
    (require 'ob-js)
    (require 'ob-typescript)
    (require 'ob-ruby)
    (add-to-list 'org-structure-template-alist '("txt"  . "src text"))
    (add-to-list 'org-structure-template-alist '("cc"   . "src C"))
    (add-to-list 'org-structure-template-alist '("sh"   . "src shell"))
    (add-to-list 'org-structure-template-alist '("zsh"  . "src zsh"))
    (add-to-list 'org-structure-template-alist '("bash" . "src bash"))
    (add-to-list 'org-structure-template-alist '("posh" . "src powershell"))
    (add-to-list 'org-structure-template-alist '("el"   . "src emacs-lisp"))
    (add-to-list 'org-structure-template-alist '("py"   . "src python"))
    (add-to-list 'org-structure-template-alist '("js"   . "src js"))
    (add-to-list 'org-structure-template-alist '("ts"   . "src typescript"))
    (add-to-list 'org-structure-template-alist '("java" . "src java"))
    (add-to-list 'org-structure-template-alist '("scm"  . "src scheme"))
    (add-to-list 'org-structure-template-alist '("sql"  . "src sql"))
    (add-to-list 'org-structure-template-alist '("rust" . "src rust"))
    (add-to-list 'org-structure-template-alist '("rb"   . "src ruby"))
    (org-babel-do-load-languages
     'org-babel-load-languages
     (seq-filter
  	(lambda (pair)
  	  (locate-library (concat "ob-" (symbol-name (car pair)))))
  	'((dot        . t)
  	  (C          . t)
  	  (gnuplot    . t)
  	  (latex      . t)
  	  (python     . t)
  	  (js         . t)
  	  (typescript . t)
  	  (shell      . t)
  	  (zsh        . t)
  	  (bash       . t)
  	  (posh       . t)
  	  (powershell . t)
  	  (scheme     . t)
  	  (sql        . t)
  	  (sqlite     . t)
  	  (ruby       . t)
  	  (emacs-lisp . t)
  	  (http       . t)
  	  (restclient . t))))
    (defun handle-electric-pair-inhibit (c)
  	(if (char-equal c ?<) t (,electric-pair-inhibit-predicate c)))
    (defun handle-org-mode-hook()
  	(setq-local electric-pair-inhibit-predicate #'handle-electric-pair-inhibit))
    (add-hook 'org-mode-hook #'handle-org-mode-hook)

    ;; Load org-remoteimg for remote image display
    (add-to-list 'load-path (expand-file-name "elisp/settings" user-emacs-directory))
    (require 'org-remoteimg))

  (use-package org-download
    :after org
    :custom
    (org-download-image-dir "images")
    (org-download-heading-lvl nil)
    (org-download-screenshot-method
     (cond ((eq system-type 'darwin) "screencapture -i %s")
           ((eq system-type 'gnu/linux) "scrot -s %s")))
    :config
    ;; Enable inline display of remote images
    (setq org-download-display-inline-images 'posframe))

  (use-package org-appear
    :hook
    (org-mode . org-appear-mode)
    :custom
    (org-appear-autoemphasis t)
    (org-appear-autolinks t)
    (org-appear-autosubmarkers t)
    (org-appear-autoentities t)
    (org-appear-autokeywords t)
    (org-appear-inside-latex t)
    (org-appear-delay 0.0)
    (org-appear-trigger 'always))

  (use-package org-gcal
    :after org
    :custom
    ;; IMPORTANT: Set these variables in your private.el or custom-vars.el:
    ;; (setq org-gcal-client-id "your-client-id-from-google-console.apps.googleusercontent.com")
    ;; (setq org-gcal-client-secret "your-client-secret-from-google-console")
    ;; (setq org-gcal-file-alist '(("your-email@gmail.com" . "~/org/gcal.org")
    ;;                              ("another-calendar@gmail.com" . "~/org/work-gcal.org")))

    ;; Directory to store OAuth token and sync metadata
    (org-gcal-dir (expand-file-name ".org-gcal/" user-emacs-directory))

    ;; Token storage - use plain text file (no .gpg extension) to avoid encryption
    ;; The token file will have restricted file permissions (600) for security
    (org-gcal-token-file (expand-file-name ".org-gcal/token" user-emacs-directory))

    ;; Automatically fetch events when opening org-agenda
    (org-gcal-auto-archive nil)

    ;; Notify on sync
    (org-gcal-notify-p t)

    ;; Update events if they already exist (bidirectional sync)
    (org-gcal-update-cancelled-events-with-todo t)

    :init
    ;; CRITICAL: Disable plstore encryption BEFORE org-gcal loads
    ;; This must be in :init, not :config
    (setq plstore-cache-passphrase-for-symmetric-encryption t)
    (setq plstore-encrypt-to nil)

    ;; Ensure EPG context doesn't prompt for passphrase
    (setq epg-pinentry-mode 'loopback)

    ;; Auto-provide a passphrase if encryption can't be disabled
    ;; This prevents repeated prompts
    (defvar my/org-gcal-passphrase "emacs-gcal-token")
    (defun my/org-gcal-passphrase-callback (context key-id handback)
      "Automatically provide passphrase for org-gcal token encryption."
      my/org-gcal-passphrase)

    ;; Override EPG passphrase callback
    (setq epg-passphrase-callback-function #'my/org-gcal-passphrase-callback)

    :config
    ;; Additional workaround: advice plstore to disable encryption
    (when (fboundp 'plstore-open)
      (advice-add 'plstore-open :around
                  (lambda (orig-fun file)
                    (let ((plstore-encrypt-to nil))
                      (funcall orig-fun file)))))
    
    ;; Add org-gcal files to org-agenda-files if configured
    (when (and (boundp 'org-gcal-file-alist) org-gcal-file-alist)
      (dolist (calendar org-gcal-file-alist)
        (let ((gcal-file (cdr calendar)))
          ;; Create the file if it doesn't exist
          (unless (file-exists-p gcal-file)
            (make-directory (file-name-directory gcal-file) t)
            (write-region "" nil gcal-file))
          ;; Add to org-agenda-files if not already there
          (when (and (boundp 'org-agenda-files)
                     (not (member gcal-file org-agenda-files)))
            (add-to-list 'org-agenda-files gcal-file)))))

    :bind
    (("C-c g p" . org-gcal-post-at-point)
     ("C-c g d" . org-gcal-delete-at-point)
     ("C-c g r" . org-gcal-refresh-token)))

  (use-package denote
    :custom
    (denote-sort-keywords t)
    (denote-directory (expand-file-name org-directory))
    (denote-prompts '(file-type subdirectory title keywords))
    :hook
    (dired-mode . denote-dired-mode))

  (provide 'note-conf)
  ;;; note-conf.el ends here
#+end_src

Reference ideas: [[https://emacselements.com/pdf-tools-settings.html][EmacsElements]]

#+begin_src emacs-lisp :tangle no
  ;;; pdf-conf.el --- Summary  -*- lexical-binding: t; -*-
  ;;; Commentary:
  ;;; Code:
  (pdf-tools-install)
  (use-package saveplace-pdf-view)
  (global-goto-address-mode t)
  (provide 'pdf-conf)
  ;;; pdf-conf.el ends here
#+end_src

#+begin_src emacs-lisp :tangle ~/.emacs.d/elisp/settings/org-remoteimg.el
  ;;; org-remoteimg.el --- Display remote inline images in org-mode -*- lexical-binding: t -*-

  ;; Copyright (C) 2023 Dean Gao - MIT License
  ;; Author: Dean Gao <gao.dean@hotmail.com>
  ;; Description: Inline display of remote images in org-mode
  ;; Homepage: https://github.com/gaoDean/org-imgtog
  ;; Package-Requires: ((emacs "25.1"))

  ;; Permission is hereby granted, free of charge, to any person obtaining a copy
  ;; of this software and associated documentation files (the "Software"), to deal
  ;; in the Software without restriction, including without limitation the rights
  ;; to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  ;; copies of the Software, and to permit persons to whom the Software is
  ;; furnished to do so, subject to the following conditions:
  ;;
  ;; The above copyright notice and this permission notice shall be included in all
  ;; copies or substantial portions of the Software.
  ;;
  ;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  ;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  ;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  ;; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  ;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  ;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  ;; SOFTWARE.

  ;;; Commentary:

  ;; This package displays remote images inline in org-mode with automatic caching.
  ;; This means you can do \[\[https://my-file.png\]\], and have it display inline.
  ;; The next time you visit the file or fetch the image, it will be instantly
  ;; fetched from the cache.

  ;;; Code:

  (require 'org)
  (require 'org-element)
  (require 'url)
  (require 'url-cache)

  (unless (fboundp 'image-supported-file-p)
    ;; `image-supported-file-p' isn't available before Emacs 28
    ;; Add alias to not break Emacs <28.
    (defalias 'image-type-from-file-name 'image-supported-file-p))

  ;; Compatibility definitions for Org-mode < 9.8
  ;; In Org < 9.8, the variable is called org-inline-image-overlays
  ;; In Org >= 9.8, it was renamed to org-link-preview-overlays
  (unless (boundp 'org-link-preview-overlays)
    (defvaralias 'org-link-preview-overlays 'org-inline-image-overlays
      "Compatibility alias for org-inline-image-overlays in Org < 9.8."))

  (defun org-image-update-overlay (file link &optional data-p refresh)
    "Create image overlay for FILE associtated with org-element LINK.
  If DATA-P is non-nil FILE is not a file name but a string with the image data.
  If REFRESH is non-nil don't download the file but refresh the image.
  See also `create-image'.
  This function is almost a duplicate of a part of `org-display-inline-images'."
    (when (or data-p (file-exists-p file))
      (let ((width
             ;; Apply `org-image-actual-width' specifications.
             (cond
              ((eq org-image-actual-width t) nil)
              ((listp org-image-actual-width)
               (or
                ;; First try to find a width among
                ;; attributes associated to the paragraph
                ;; containing link.
                (let ((paragraph
                       (let ((e link))
                         (while (and (setq e (org-element-property
                                              :parent e))
                                     (not (eq (org-element-type e)
                                              'paragraph))))
                         e)))
                  (when paragraph
                    (save-excursion
                      (goto-char (org-element-property :begin paragraph))
                      (when
                          (re-search-forward
                           "^[ \t]*#\\+attr_.*?: +.*?:width +\\(\\S-+\\)"
                           (org-element-property
                            :post-affiliated paragraph)
                           t)
                        (string-to-number (match-string 1))))))
                ;; Otherwise, fall-back to provided number.
                (car org-image-actual-width)))
              ((numberp org-image-actual-width)
               org-image-actual-width)))
            (old (get-char-property-and-overlay
                  (org-element-property :begin link)
                  'org-image-overlay)))
        (if (and (car-safe old) refresh)
            (image-flush (overlay-get (cdr old) 'display))
          (let ((image (create-image file
                                     (and (image-type-available-p 'imagemagick)
                                          width
                                          'imagemagick)
                                     data-p
                                     :width width)))
            (when image
              (let* ((link
                      ;; If inline image is the description
                      ;; of another link, be sure to
                      ;; consider the latter as the one to
                      ;; apply the overlay on.
                      (let ((parent
                             (org-element-property :parent link)))
                        (if (eq (org-element-type parent) 'link)
                            parent
                          link)))
                     (ov (make-overlay
                          (org-element-property :begin link)
                          (progn
                            (goto-char
                             (org-element-property :end link))
                            (skip-chars-backward " \t")
                            (point)))))
                (overlay-put ov 'display image)
                (overlay-put ov 'face 'default)
                (overlay-put ov 'org-image-overlay t)
                (overlay-put
                 ov 'modification-hooks
                 (list 'org-display-inline-remove-overlay))
                (push ov org-link-preview-overlays)
                ov)))))))

  (defun org-display-user-inline-images (&optional _include-linked _refresh beg end)
    "Like `org-display-inline-images' but for image data links.
  _INCLUDE-LINKED and _REFRESH are ignored.
  Restrict to region between BEG and END if both are non-nil.
  Image data links have a :image-data-fun parameter.
  \(See `org-link-set-parameters'.)
  The value of the :image-data-fun parameter is a function
  taking the PROTOCOL, the LINK, and the DESCRIPTION as arguments.
  If that function returns nil the link is not interpreted as image.
  Otherwise the return value is the image data string to be displayed.

  Note that only bracket links are allowed as image data links
  with one of the formats
   [[PROTOCOL:LINK]]
  or
   [[PROTOCOL:LINK][DESCRIPTION]]
  are recognized.
  Full credit goes to org-yt by Tobias Zawada for this function."
    (interactive)
    (when (and (called-interactively-p 'any)
               (use-region-p))
      (setq beg (region-beginning)
            end (region-end)))
    (when (display-graphic-p)
      (org-with-wide-buffer
       (goto-char (or beg (point-min)))
       (when-let* ((image-data-link-parameters
  		 (cl-loop for link-par-entry in org-link-parameters
  			  with fun
  			  when (setq fun (plist-get (cdr link-par-entry) :image-data-fun))
  			  collect (cons (car link-par-entry) fun)))
  		(image-data-link-re (regexp-opt (mapcar 'car image-data-link-parameters)))
  		(re (format "\\[\\[\\(%s\\):\\([^]]+\\)\\]\\(?:\\[\\([^]]+\\)\\]\\)?\\]"
  			    image-data-link-re)))
         (while (re-search-forward re end t)
           (let* ((protocol (match-string-no-properties 1))
  		(link (match-string-no-properties 2))
  		(description (match-string-no-properties 3))
  		(image-data-link (assoc-string protocol image-data-link-parameters))
  		(el (save-excursion (goto-char (match-beginning 1)) (org-element-context)))
  		image-data)
             (when el
               (setq image-data
                     (or (let ((old (get-char-property-and-overlay
                                     (org-element-property :begin el)
                                     'org-image-overlay)))
                           (and old
                                (car-safe old)
                                (overlay-get (cdr old) 'display)))
  		       (funcall (cdr image-data-link) protocol link description)))
               (when image-data
                 (let ((ol (org-image-update-overlay image-data el t t)))
                   (when (and ol description)
                     (overlay-put ol 'after-string description)))))))))))

  (defun org-remoteimg--fetch-image (protocol link _description)
    "Synchronously retrieve image from cache or web."
    (when (and (image-supported-file-p link)
               (not (eq org-display-remote-inline-images 'skip)))
      (let* ((cache (eq org-display-remote-inline-images 'cache))
             (url-automatic-caching (if cache
                                        t
                                      url-automatic-caching))
             (url (concat protocol ":" link))
             (silent-output (file-exists-p (url-cache-create-filename url))))
        (if-let* ((buf (url-retrieve-synchronously url
                                                     silent-output
                                                     nil
                                                     30)))
                (with-current-buffer buf
                  (goto-char (point-min))
                  (re-search-forward "\r?\n\r?\n" nil t)
                  (buffer-substring-no-properties (point) (point-max)))
              (error "Download of image \"%s\" failed" link)
              nil))))

  (defun org-link-preview-http (ov _path link)
    "Generate preview OV for LINK in PATH."
    (when-let* ((raw-link (org-element-property :raw-link link))
                (image-type (image-supported-file-p raw-link))
                ;; cache?
                (image-buffer (org-remoteimg--fetch-image nil raw-link nil)))
          (overlay-put ov 'display (create-image image-buffer
                                        ;; scale?
                                     ;; (and (image-type-available-p 'imagemagick)
                                     ;;      width
                                     ;;      'imagemagick)
                                                               ;; :width width
                                                               image-type
                                                               t))
          t))

  (if (fboundp 'org-display-inline-images)
      (progn
        (advice-add #'org-display-inline-images :after #'org-display-user-inline-images)
        (org-link-set-parameters "http"  :image-data-fun #'org-remoteimg--fetch-image)
        (org-link-set-parameters "https" :image-data-fun #'org-remoteimg--fetch-image))
    (org-link-set-parameters "http" :preview #'org-link-preview-http)
    (org-link-set-parameters "https" :preview #'org-link-preview-http))

  (provide 'org-remoteimg)

  ;;; org-remoteimg.el ends here

#+end_src

